#Setters and Getters

In this step, we will make the fields in Spot private and then introduce the *setter* and *getter* methods.  This will allow us implement the encapsulation rules in our class.  


##Spot V7.0

Save your **Spot_Version6_1** work as **Spot_Version7_0**.

Refactor your Spot class so that the fields have private access and it has the following setter and getter methods:

~~~java
class Spot {

  private float xCoord, yCoord;
  private float diameter;
  private int red, green, blue, gray;

  //------------------//
  //   constructors   //
  //------------------//

  Spot()
  {
  }

  Spot(float xCoord, float yCoord, float diameter)
  {
    this.xCoord = xCoord;
    this.yCoord = yCoord;
    this.diameter = diameter;
  }

  Spot(float xCoord, float yCoord, float diameter, int gray)
  {
    this.xCoord = xCoord;
    this.yCoord = yCoord;
    this.diameter = diameter;
    colour(gray);
  }

  Spot(float xCoord, float yCoord, float diameter, int red, int green, int blue)
  {
    this.xCoord = xCoord;
    this.yCoord = yCoord;
    this.diameter = diameter;
    colour(red, green, blue);
  }

  //------------------//
  //     methods      //
  //------------------//

  void display()
  {
    ellipse(xCoord, yCoord, diameter, diameter);
  } 

  void colour(int red, int green, int blue)
  {
    this.red = red;
    this.green = green;
    this.blue = blue;
    fill (this.red, this.green, this.blue);
  }

  void colour(int gray) {
    this.gray = gray;
    fill (this.gray);
  }

  void move(float xCoord, float yCoord)
  {
    this.xCoord = xCoord;
    this.yCoord = yCoord;
  }

  //------------------//
  //     getters      //
  //------------------//
  public float getDiameter() {
    return diameter;
  }

  public float getXCoord() {
    return xCoord;
  }

  public float getYCoord() {
    return yCoord;
  }

  public int getRed() {
    return red;
  }

  public int getGreen() {
    return green;
  }

  public int getBlue() {
    return blue;
  }

  public int getGray() {
    return gray;
  }

  //------------------//
  //     setters      //
  //------------------//
  public void setDiameter(float diameter) {
    this.diameter = diameter;
  } 

  public void setXCoord(float xCoord) {
    this.xCoord = xCoord;
  } 

  public void setYCoord(float yCoord) {
    this.yCoord = yCoord;
  } 

  public void setRed(int red) {
    this.red = red;
  } 

  public void setGreen(int green) {
    this.green = green;
  } 

  public void setBlue(int blue) {
    this.blue = blue;
  }

  public void setGray(int gray) {
    this.gray = gray;
  }

}
~~~

and then in the *main* tab, refactor the draw() method to: 

~~~java
Spot sp;

void setup()
{
  size (100,100);
  noStroke();

  sp = new Spot(33, 50, 30);
  println("After constructor, the value of the diameter is " + sp.getDiameter());

  // Now change the value of the diameter
  sp.setDiameter(40);
  println("After setter, the value of the diameter is " + sp.getDiameter());
}

void draw()
{
  background(0);
  sp.colour(255,0,0);
  sp.display();
}
~~~


Examine the Spot code i.e. the getters and setters. Note what each of the methods do, their naming convention and return values etc.

Run this code.  

Your console output should be:

~~~
After constructor, the value of the diameter is 30.0
After setter, the value of the diameter is 40.0
~~~

Have a look at your code.  Are you happy that you understand how this output was produced? 

Save your work (don't close the project though).


