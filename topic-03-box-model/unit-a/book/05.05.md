#Setters and Getters - validations

In this step, we will add some validation to our diameter field.


##Spot V7.1 - Using validation in **constructors**
 
Save your **Spot_Version7_0** work as **Spot_Version7_1**.

Refactor the following Spot Constructor so that it validates the diameter field.

~~~java 
Spot(float xCoord, float yCoord, float diameter)
  {
    this.xCoord = xCoord;
    this.yCoord = yCoord;
    if ((diameter >= 20) && (diameter <= 50)){
       this.diameter = diameter;
    }
    else{
       this.diameter = 20;
    } 
  }
~~~

This code will only allow values of between (and including) 20 and 50 into the diameter field. If values outside this range are sent in via the parameter, then the value of 20 is assigned to the diameter field (using the else clause).

To see this validation in action, refactor the setup method in the *main* tab to:

~~~java
Spot sp;

void setup()
{
  size (100,100);
  noStroke();
  
  //constructor (float, float, float) test
  sp = new Spot(33, 50, 30000);
  println("The value of diameter is : "+ sp.getDiameter());

}

void draw()
{
  background(0);
  sp.display();
  sp.move(mouseX,  mouseY);
}
~~~

Try to run the code. You should see the following line in the console screen:

![Value of diameter field](./img/05.png)


##Data Integrity

We have diameter validation in one constructor, but we can see that the diameter is updated in other constructors:

~~~java 
  //------------------//
  //   constructors   //
  //------------------//

  Spot()
  {
  }

  Spot(float xCoord, float yCoord, float diameter)
  {
    this.xCoord = xCoord;
    this.yCoord = yCoord;
    if ((diameter >= 20) && (diameter <= 50)) {
      this.diameter = diameter;
    } else {
      this.diameter = 20;
    }
  }

  Spot(float xCoord, float yCoord, float diameter, int gray)
  {
    this.xCoord = xCoord;
    this.yCoord = yCoord;
    this.diameter = diameter;
    colour(gray);
  }

  Spot(float xCoord, float yCoord, float diameter, int red, int green, int blue)
  {
    this.xCoord = xCoord;
    this.yCoord = yCoord;
    this.diameter = diameter;
    colour(red, green, blue);
  }

~~~

It is also updated in the mutator.  

~~~java
  public void setDiameter(float diameter) {
    this.diameter = diameter;
  } 
~~~

We have compromised our *data integrity*...diameter may or may not be between the right boundaries, depending on which constructor/mutator we call.  We have to fix this!

Copy and paste the constructor diameter validation into all constructors that update diameter:

~~~java
  Spot(float xCoord, float yCoord, float diameter)
  {
    this.xCoord = xCoord;
    this.yCoord = yCoord;
    if ((diameter >= 20) && (diameter <= 50)) {
      this.diameter = diameter;
    } else {
      this.diameter = 20;
    }
  }

  Spot(float xCoord, float yCoord, float diameter, int gray)
  {
    this.xCoord = xCoord;
    this.yCoord = yCoord;
    if ((diameter >= 20) && (diameter <= 50)) {
      this.diameter = diameter;
    } else {
      this.diameter = 20;
    }
    colour(gray);
  }

  Spot(float xCoord, float yCoord, float diameter, int red, int green, int blue)
  {
    this.xCoord = xCoord;
    this.yCoord = yCoord;
    if ((diameter >= 20) && (diameter <= 50)) {
      this.diameter = diameter;
    } else {
      this.diameter = 20;
    }
    colour(red, green, blue);
  }
~~~

Paste the constructor diameter validation into the diameter mutator and remove the *else* clause:

~~~java
  public void setDiameter(float diameter) {
   if ((diameter >= 20) && (diameter <= 50)) {
      this.diameter = diameter;
    }
  } 
~~~

A note on setters:  

- We use setters as the *safe* way to change the values of fields after the object has been created.
- We typically use the same validation rules that we applied in the constructor...except...there typically is ***no*** else in the if statement. This means that if the parameter is not in the correct range, then the diameter field will remain unchanged. Where feasible, this is the convention we use for this module. 


##Testing the grayscale constructor and the mutator

To test your grayscale constructor and the mutator code, refactor your setup method in the *main* tab to:

~~~java
void setup()
{
  size (100, 100);
  noStroke();
  
  //grayscale constructor test
  sp = new Spot(33, 50, 30000, 200);
  println("After grayscale constructor, the value of the diameter is " + sp.getDiameter());
  sp.setDiameter(40000);    // changing the value of the diameter
  println("After setter, the value of the diameter is " + sp.getDiameter());

}
~~~

Run your code.  The console output should be:

~~~
After grayscale constructor, the value of the diameter is 20.0
After setter, the value of the diameter is 20.0
~~~


##Testing the rgb constructor and the mutator

To test your rgb constructor and the mutator code, refactor your setup method in the *main* tab to:

~~~java
void setup()
{
  size (100, 100);
  noStroke();

  //rgb constructor test
  sp = new Spot(33, 50, 30000, 200, 0, 0);
  println("After rgb constructor, the value of the diameter is " + sp.getDiameter());
  sp.setDiameter(40000);    // changing the value of the diameter
  println("After setter, the value of the diameter is " + sp.getDiameter());

}
~~~

Run your code.  The console output should be:

~~~
After rgb constructor, the value of the diameter is 20.0
After setter, the value of the diameter is 20.0
~~~

Save your work and close the project.


##Repeating code...

You may have noticed that we have repeated the same validation in many places in the above code.  This is not the best approach at all.  What would happen if we decided to change the diameter boundaries of the Spot from 20:50 to 20:100?  You would have to *remember* to change it in four places.  That's a lot of remembering and this is where bugs sneak in...what if we forgot to change one of the checks?   

A key mantra to remember when coding...use the **DRY** approach...**Don't Repeat Yourself**.  However, we have a bit of work to do before we move down this path.  Later in the module, we will show a different approach to avoid this scenario and adhere to DRY principles.   

